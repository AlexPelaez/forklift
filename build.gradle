/*
 * Forklift - An enterprise service bus that supports building and deploying
 * microservice consumer and producers quickly and easily by annotating code.
 *
 * Composed of several sub-projects:
 *    - core: the core forklift code
 *    - connectors: ActiveMQ and Kafka connectors for producing and consuming on the different brokers
 *    - plugins: Different handlers that can be used to extend the forklift core
 *    - server: A java executable runnable container similar to Tomcat that can run consumers
 *    - consumers: Examples written to show how you to build your own forklift ESB
 */

def repoUser = System.getenv('SONATYPE_USER') ?: ''
def repoPassword = System.getenv('SONATYPE_PASS') ?: ''

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://packages.confluent.io/maven'
        }
        maven {
            name 'Sonatype OSS Snapshots'
            url 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username repoUser
                password repoPassword
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    group = 'com.github.dcshock'
    version = '3.8-SNAPSHOT'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    jar {
        manifest {
            attributes("Implementation-Title": "forklift",
                       "Implementation-Version": version)
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    url = 'https://github.com/dcshock/forklift</'
                    licenses {
                        license {
                            name = 'BSD-style'
                            url = 'http://www.opensource.org/licenses/bsd-license.php'
                        }
                    }
                    scm {
                        url = 'git@github.com:dcshock/forklift.git'
                        connection = 'scm:git:git@github.com:dcshock/forklift.git'
                    }
                    developers {
                        developer {
                            id = 'dcshock'
                            name = 'Matt Conroy'
                            url = 'http://www.mattconroy.com'
                        }
                        developer {
                            id = 'afrieze'
                            name = 'Andrew Frieze'
                        }
                        developer {
                            id = 'kuroshii'
                            name = 'Bridger Howell'
                        }
                        developer {
                            id = 'applitect'
                            name = 'David Thompson'
                        }
                    }
                }
            }
        }
    }
}
